data cms.all_mbsf;
    set cms.mbsf2014 cms.mbsf2015 cms.mbsf2016 cms.mbsf2017 cms.mbsf2018 cms.mbsf2019;
    keep bene_id bene_enrollmt_ref_yr 
         mdcr_entlmt_buyin_ind_01-mdcr_entlmt_buyin_ind_12 
         hmo_ind_01-hmo_ind_12 
         ptc_pbp_id_01-ptc_pbp_id_12 
         ptd_cntrct_id_01-ptd_cntrct_id_12;
run;


**CONTINUOUS ENROLLMENT;
%macro _5pbenesum( yr, indata);
data benesum&yr (keep = bene_id mdcr_entlmt_buyin_ind_01-mdcr_entlmt_buyin_ind_12 hmo_ind_01-hmo_ind_12  HMOIND_&yr BUYIN_&yr);
  set cms.&indata;
  HMOIND_&yr = compress(hmo_ind_01||hmo_ind_02||hmo_ind_03||hmo_ind_04||hmo_ind_05||hmo_ind_06||hmo_ind_07||hmo_ind_08||hmo_ind_09||hmo_ind_10||hmo_ind_11||hmo_ind_12, " ");
  BUYIN_&yr = compress(mdcr_entlmt_buyin_ind_01||mdcr_entlmt_buyin_ind_02||mdcr_entlmt_buyin_ind_03||mdcr_entlmt_buyin_ind_04||mdcr_entlmt_buyin_ind_05||mdcr_entlmt_buyin_ind_06||mdcr_entlmt_buyin_ind_07||mdcr_entlmt_buyin_ind_08||mdcr_entlmt_buyin_ind_09||mdcr_entlmt_buyin_ind_10||mdcr_entlmt_buyin_ind_11||mdcr_entlmt_buyin_ind_12, " ");
run;

proc sort data=benesum&yr; by bene_id; run;

Proc sql;
create table  benesum&yr._1 as
select a.bene_id, a.HMOIND_&yr, a.BUYIN_&yr
from benesum&yr as a, cms.nopriorpd as b
where a.bene_id = b.bene_id;
quit;

Proc sort data = benesum&yr._1 nodupkey; by bene_id; run;

%mend _5pbenesum;

%_5pbenesum (2015, mbsf2015);	
%_5pbenesum (2016, mbsf2016);	
%_5pbenesum (2017, mbsf2017);	
%_5pbenesum (2018, mbsf2018);		
%_5pbenesum (2019, mbsf2019);			

data benesum_2015_2019 ;				
  merge benesum2015_1 benesum2016_1 benesum2017_1 benesum2018_1 benesum2019_1;
  by bene_id;
run;

Proc sql;
create table raw_cohort1 as
select * 
from  benesum_2015_2019 as a,  cms.nopriorpd as b
where a.bene_id = b.bene_id;
quit;

data  raw_cohort2; 
  set raw_cohort1;

   HMOIND = HMOIND_2015||HMOIND_2016||HMOIND_2017||HMOIND_2018||HMOIND_2019;
   BUYIN = BUYIN_2015||BUYIN_2016||BUYIN_2017||BUYIN_2018||BUYIN_2019;

	PartAB365 = 0;						
	HMO365=0;				

    Start  = (year(index_date)-2015)*12 + month(index_date) -12;
	end  = (year(index_date)-2015)*12 + month(index_date) - 1;

	**365 days prior to index date;
	do i = start to end until (substr (BUYIN,i,1) not in ("3","C"));
			if substr(BUYIN,i,1)in ("3","C")then PartAB365 + 1; end;

	do j=start to end until (substr(HMOIND,j,1)not in ("0"));
		if substr(HMOIND,j,1) in ("0")then HMO365+1; end;	

	if partAB365 = (end-start)+1 and hmo365 = (end-start) + 1 then cont_12mon = 1; else cont_12mon = 0;

run;


data raw_cohort3; 	
set raw_cohort2;
where cont_12mon = 1;
run;
