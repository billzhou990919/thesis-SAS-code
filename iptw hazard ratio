/* Add time from cancer diagnosis to index date to dataset */

proc univariate data=mscan.analytical_cohort; var survival_time; histogram; run;

%let covaraites = age sex racem_black racem_hispanic racem_white racem_other maritalm dualflag
 				  regionm_midwest regionm_missing regionm_northeast regionm_south regionm_west
				  /*rank_edu_char_0 rank_edu_char_1 rank_edu_char_2 rank_edu_char_3 rank_edu_char_Missing*/
				  rank_income_char_0 rank_income_char_1 rank_income_char_2 rank_income_char_3 rank_income_char_Missing
				  cancer_type_bladder_cancer cancer_type_breast_cancer cancer_type_colorectal_cancer
				  cancer_type_esophagus_cancer cancer_type_kidney_cancer cancer_type_lung_cancer
				  cancer_type_ovary_cancer cancer_type_pancreas_cancer cancer_type_prostate_cancer
				  cancer_type_stomach_cancer cancer_type_uterus_cancer
				  number_prior_hospitalization number_prior_physician
				  chemo				  
/*				  init_yr_2010 init_yr_2011 init_yr_2012 init_yr_2013 init_yr_2014 init_yr_2015 init_yr_2016*/
				  stage_stage_0 stage_stage_I stage_stage_II stage_stage_III stage_stage_IV stage_unknown
				  grade_grade_I grade_grade_II grade_grade_III grade_grade_IV grade_T_cell_B_cell_cell_type_no
				  tumor_size_0cm tumor_size_0cm___tumor_size___1c tumor_size_1cm___tumor_size___2c
				  tumor_size_2cm___tumor_size___3c tumor_size_3cm___tumor_size___4c tumor_size_4cm___tumor_size___5c
				  tumor_size_NA_Site_specific_code tumor_size_tumor_size___5cm
				  
				  CHF HT STT VD Dia AKF ALF bleed alcohol
				  anemia asthma COPD dementia gout HL IA Other_IHD Other_CD PUD CR
				  Home_O2 Osteoporotic_frac Walker Wheelchair Home_hosp_bed fall
				  acei arb av antiarrhythmics antiplatelets aspirin bb ccb diuretics oa dd estrogens progestins hlmwh naid statins nlld ppi;



proc psmatch data=comp_eff.event_12_new region=allobs;
		class doac_flag sex maritalm dualflag chemo
			  racem_black racem_hispanic racem_white racem_other
			  regionm_midwest regionm_missing regionm_northeast regionm_south regionm_west
			  cancer_type_bladder_cancer cancer_type_breast_cancer cancer_type_colorectal_cancer
			  cancer_type_esophagus_cancer cancer_type_kidney_cancer cancer_type_lung_cancer
			  cancer_type_ovary_cancer cancer_type_pancreas_cancer cancer_type_prostate_cancer
			  cancer_type_stomach_cancer cancer_type_uterus_cancer
/*			  init_yr_2010 init_yr_2011 init_yr_2012 init_yr_2013 init_yr_2014 init_yr_2015 init_yr_2016*/
			  stage_stage_0 stage_stage_I stage_stage_II stage_stage_III stage_stage_IV stage_unknown
			  grade_grade_I grade_grade_II grade_grade_III grade_grade_IV grade_T_cell_B_cell_cell_type_no
			  tumor_size_0cm tumor_size_0cm___tumor_size___1c tumor_size_1cm___tumor_size___2c
			  tumor_size_2cm___tumor_size___3c tumor_size_3cm___tumor_size___4c tumor_size_4cm___tumor_size___5c
			  tumor_size_NA_Site_specific_code tumor_size_tumor_size___5cm 
/*			  rank_edu_char_0 rank_edu_char_1 rank_edu_char_2 rank_edu_char_3 rank_edu_char_Missing*/
			  rank_income_char_0 rank_income_char_1 rank_income_char_2 rank_income_char_3 rank_income_char_Missing
			  CHF HT STT VD Dia AKF ALF bleed alcohol
			  anemia asthma COPD dementia gout HL IA Other_IHD Other_CD PUD CR
			  Osteoporotic_frac fall
/*			  Home_O2  Walker Wheelchair Home_hosp_bed */
			  acei arb av antiarrhythmics antiplatelets aspirin bb ccb diuretics oa dd estrogens progestins hlmwh naid statins nlld ppi;
		psmodel doac_flag(treated='1')= &covaraites;
		assess lps var=(&covaraites)
						/ varinfo nlargestwgt=6 plots=(barchart boxplot(display=(lps )) wgtcloud) weight=atewgt(stabilize=yes);
output out(obs=all)=comp_eff.propensity_score atewgt(stabilize=yes)=_ATEWgt_;
run;
proc univariate data=comp_eff.propensity_score; var _ps_; class doac_flag; histogram; run; /*propensity score*/
proc univariate data=comp_eff.propensity_score; var _ATEWgt_; histogram; run; /*stabilized weights*/
proc sgplot data=comp_eff.propensity_score;       
  histogram _ps_ / group=doac_flag transparency=0.5;     
  density _ps_ / type=kernel group=doac_flag; 
run;
proc sgplot data=comp_eff.propensity_score;          
  density _ps_ / type=kernel group=doac_flag; 
run;
/*Trimming at 1st and 99th percentile*/
data comp_eff.propensity_score_trimmed; 
set comp_eff.propensity_score; 
if 0< _ATEWgt_ < 0.510683 then _ATEWgt_=0.510683; 
if _ATEWgt_ > 2.891309 then _ATEWgt_=2.891309; 
if _ATEWgt_=. then delete; 
run;
proc sgplot data=comp_eff.propensity_score_trimmed;       
  histogram _ATEWgt_ / group=doac_flag transparency=0.5;     
  density _ATEWgt_ / type=kernel group=doac_flag; 
run;
proc sgplot data=comp_eff.propensity_score_trimmed;           
  density _ATEWgt_ / type=kernel group=doac_flag; 
run;
proc sgplot data=comp_eff.propensity_score_trimmed;       
  histogram _ps_ / group=doac_flag transparency=0.5;     
  density _ps_ / type=kernel group=doac_flag; 
run;



****************************** Primary outcome: Time to ischemic stroke or systemic embolism ******************************************************************;

proc freq data=comp_eff.propensity_score;
	table (censor_flag censor_flag_2 censor_flag_pneumonia_2)*doac_flag;
run;
/***. Unadjusted Hazard ratio for ischemic stroke or systematic embolism - competing risk model*/
proc phreg data=comp_eff.propensity_score;
	class doac_flag / param=ref ref=first; 
	model time_to_stroke_embolism*censor_flag(0) = doac_flag / eventcode=1 risklimits;
run;

proc lifetest data=comp_eff.event_1 plots=cif(test); *Cumulative incidence curve for ischemic stroke or systemic embolism;
   time time_to_stroke_embolism*censor_flag(0)/eventcode=1;
   strata doac_flag / order=internal;
run;

**2. Trimmed IPTW association of ischemic stroke or systematic embolism;
proc phreg data=comp_eff.propensity_score_trimmed;
	class doac_flag(ref='0') init_yr(ref='2016')/ param=ref;
	model time_to_stroke_embolism*censor_flag(0) = doac_flag init_yr time_cancer_to_index/ eventcode=1 risklimits;
	weight _ATEWgt_ / normalize;
run;
data treat;
doac_flag = 1; output;
doac_flag = 0; output; run;
proc phreg data=comp_eff.propensity_score_trimmed  plots(overlay)=cif;
	class doac_flag(ref='0') init_yr(ref='2016')/ param=ref;
	model time_to_stroke_embolism*censor_flag(0) = doac_flag/ eventcode=1 risklimits;
	weight _ATEWgt_ / normalize;
	baseline covariates=treat  / rowid=doac_flag;
run;
